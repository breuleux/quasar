

require:
   .directory ->
      Directory
   "./proxy" ->
      Patch
      proxy, deprox, reprox
   .relevant ->
      iterate-intersect
      intersect2
   .iter ->
      iterate-properties2
   .util ->
      identify, fork-identify, tuck
      next-id, fields, methods
      ID, FORKID, DIRECTORY, CACHE
   "./root" ->
      Root

provide:
   arg-key, args-key
   task-function
   opaque-task-function
   State, DependentState
   transact-all


;;;;;;;;;;;;;;
;; arg-keys ;;
;;;;;;;;;;;;;;

arg-key{match x} =
   ;; Translate some argument x to a string key.
   String? ->
      "S" + x
   null? or undefined? or Number? or Boolean? ->
      "P" + String{x}
   {^fields.opaque => true?} and identify! and fork-identify! ->
      "O" + String{x[ID]} + ":" + String{x[FORKID]}
   deprox! [x and {^ID => Number?}] ->
      fork-identify{x}
      "O" + String{x[ID]} + ":" + String{x[FORKID]}
   else ->
      '\x03{xs.join{"\x01"}}\x04' where
         xs = items{x} each {k, v} ->
            '{k}\x02{arg-key{v}}'

args-key{args} =
   ;; Translate a list of arguments to a string key.
   args.map{arg-key}.join{"\x00"}

get-replacement{match cached} =
   {=> replacement} ->
      get-replacement{replacement}
   else ->
      cached



;;;;;;;;;;;;
;; Tracks ;;
;;;;;;;;;;;;


class Counter:
   constructor{} =
      @n-tracks = 0
   mark{} =
      @n-tracks += 1
   unmark{} =
      @n-tracks -= 1
   merge{other} =
      @n-tracks += other.n-tracks
   empty{} =
      @n-tracks <= 0
   size{} =
      1


class Chirurgies:
   constructor{} =
      @refs = new Map{}
   mark{{g, c}} =
      @refs.set{g, c}
   merge-one{counts, {g, c}} =
      if counts.n-tracks > 0:
         @refs.set{g, c}
   merge{other} =
      other.refs.entries{} each {g, c} -> @refs.set{g, c}
   unmerge-one{counts, {g, _}} =
      @refs.delete{g}
   unmerge{other} =
      other.refs.entries{} each {g, _} -> @refs.delete{g}
   unmerge-counts{other} =
      if other.n-tracks > 0:
         @refs = new Map{}
   empty{} =
      @refs.size === 0
   size{} =
      @refs.size


class Tracks:

   constructor{@factory = Counter} =
      @data = {=}

   get{id} =
      id and [@data[id] or= {=}]
   get-for{obj} =
      if not obj: return null
      id = obj[ID]
      id and [@data[id] or= {=}]

   list{} =
      items{@data}
   empty{} =
      return keys{@data}.length === 0

   mark{obj, key} =
      tracks = @get-for{obj}
      if not tracks:
         return
      t = [tracks[key] or= @factory{}]
      t.mark{}

   unmark{obj, key} =
      tracks = @get-for{obj}
      if not tracks:
         return
      t = tracks[key]
      t.unmark{}
      if t.empty{}:
         delete tracks[key]

   merge{tracks, merger = .merge, arg = null} =
      items{tracks.data} each {id, v} ->
         v0 = [@data[id] or= {=}]
         items{v} each {key, obj} ->
            obj0 = [v0[key] or= @factory{}]
            obj0[merger]{obj, arg}
            if obj0.empty{}:
               delete v0[key]
         if keys{v0}.length === 0:
            delete @data[id]

   register{obj, key, fn} =
      val = fn{}
      if val !== true:
         throw E.cannot-assimilate-tracks{fn{}}
      @mark{obj, key}

   size{} =
      var sz = 0
      items{@data} each {id, ob} ->
         items{ob} each {k, v} ->
            sz += v.size{}
      sz


;;;;;;;;;;;;
;; Opaque ;;
;;;;;;;;;;;;

opaque = Symbol{"opaque"}
contents = Symbol{"contents"}

extract-opaque{match x} =
   Opaque? -> x[contents]
   else -> x

class Opaque:
   constructor{match value} =
      String? or Number? or Boolean? or Symbol? or null? or undefined? ->
         throw E.opaque{'Opaque function cannot return a primitive value like \'{value}\''}
      {^opaque => true?} ->
         throw E.opaque{'Opaque function must own the values it returns. You cannot return the result of another opaque call.'}
      else ->
         value[opaque] = [@]
         @[contents] = value
   [^methods.iterate-properties]{} =
      {{contents, @[contents], null}}
   [^methods.fork]{dir} =
      rval = Object.create{Opaque}
      {ID, CACHE} each field ->
         tuck{rval, field, @[field]}
      fork-identify{rval}
      dir.enter{rval, false}
      rval[contents] = dir.acquire{@[contents]}
      rval

   ;; [^methods.iterate-properties2]{} =
   ;;    {{contents, @[contents], null, null}}


;;;;;;;;;;;;;;;;;;
;; CachedResult ;;
;;;;;;;;;;;;;;;;;;

class CachedResult:

   constructor{@reactor, @self, @args, @value, @tracks, @version} =
      pass

   update{} =
      current-context{} as {
         => patch
         => out-patch
         dir => let dir and {=> version}
      }
      if not patch:
         return [@]
      match version:
         == @version or when not @version ->
            @
         when @tracks and intersect2{@tracks, patch, dir} ->
            rval and @replacement = @reactor.render{@self, @args, true}
            rval
         else ->
            @


frame-stack = {}

current-frame{} =
   frame-stack[frame-stack.length - 1]

current-context{} =
   current-frame{}??.context

with-frame{frame, fn} =
   frame-stack.push{frame}
   res = fn{}
   frame-stack.pop{}
   res

class Frame:
   constructor{@tracks, context = {=}} =
      prev = [current-context{} or {=}]
      @context = [prev & {root = false}] & context


_reprox{match x, deps} =
   {^ID => Number?} or null? or undefined? or {^fields.opaque => true?} ->
      reprox{x, deps, null}
   Array? ->
      x each y -> _reprox{y, deps}
   else ->
      rval = Object.create{Object.get-prototype-of{x}}
      items{x} each {k, v} ->
         rval[k] = _reprox{v, deps}
      rval


class Reactor:

   constructor{@renderfn} =
      @argmap = {=}

   render{the-self, args, force = false} =
      key = args-key{{the-self} ++ args}
      cached =
         match @argmap[key]:
            undefined? or when force ->
               @render-cache-object{the-self, args}
            cached ->
               c = get-replacement{cached}
               c.update{}
      @argmap[key] = cached
      parent = current-frame{}
      current-context{} as {=> merge-opaque = true}
      if merge-opaque or not [Opaque? cached.value]:
         parent??.tracks.merge{cached.tracks}
      cached

   render-cache-object{the-self, args} =
      tracks = Tracks{}
      pargs = args each arg -> _reprox{arg, tracks}
      pself = reprox{the-self, tracks, null}
      current-context{} as {
         dir => {=> version = null}
         => out-dir
      }

      var res = with-frame{Frame{tracks}} with ->
         identify{@renderfn.apply{pself, pargs}}

      res = out-dir.acquire{res}

      cached = CachedResult{@, the-self, args, res, tracks, version}
      cached



;;;;;;;;;;;
;; State ;;
;;;;;;;;;;;

class State:

   constructor{@origin, @options = {=}} =
      {clobber-patch => @clobber-patch = false} = @options
      @listeners = {}

      @patch-stack = {}
      @patch = Patch{}

      @dir = Directory{clobber-patch = @clobber-patch}
      @basis = @dir.acquire-copy{@origin}
      @basis-proxy = proxy{@basis, null, @patch}
      @patch.shift{}

      State.all.push{@}

   add-listener{listener} =
      @listeners.push with listener

   get{} =
      @basis

   model{} =
      @basis-proxy

   commit{} =
      patch = Patch{@patch.shift{}}
      @dir.patch{patch}
      @listeners each listener ->
         listener{@model{}, patch}
      true

   transact{fn} =
      fn{@model{}}
      @commit{}

State.all = {}

var is-transaction = false

transact-all{fn} =
   commit = not is-transaction
   is-transaction = true
   try:
      fn{}
      if commit:
         State.all each state ->
            state.commit{}
   finally:
      if commit:
         is-transaction = false



;;;;;;;;;;;;;;;;;;;;
;; DependentState ;;
;;;;;;;;;;;;;;;;;;;;

class DependentState < State:

   constructor{@input, @render, @options = {=}} =

      @options as {
         clobber-patch => @clobber-patch = true
         action => @action = {x} -> x
      }
      @listeners = {}
      @direct-tracks = Tracks{Chirurgies}

      @patch-stack = {}
      @patch = Patch{}

      @dir = Directory{clobber-patch = @clobber-patch}

      @redo = true
      @compute{}

      @input.add-listener{@listen.bind{@}}

      State.all.push{@}

   walk{v} =
      iterate-properties2{v} each
         {k, match obj, setter, gen-patch, ignore-children = false} ->
            match Opaque? opq ->
               do:
                  gen-patch{@patch, extract-opaque{opq}}
               {^CACHE => cache} when cache ->
                  @direct-tracks.merge{cache.tracks, .merge-one} with
                     {'{v[ID]}\x00{k}', {v, k, gen-patch, cache}}
                  @walk{obj}
               else ->
                  @walk{obj}
            else ->
               @walk{obj}

   compute{patch = null} =
      if not @redo and @_previous-input !== @input.get{}:
         @redo = true
      @_previous-input = @input.get{}
      @basis =
         with-frame{Frame{Tracks{}, context}, fn} where
            context = {
               state = @input
               patch = patch
               dir = @input.dir
               out-patch = @patch
               out-dir = @dir
               root = true
            }
            fn{} =
               if @redo:
                  @redo = false
                  @render.rend.call{null, @input.model{}}
               else:
                  @basis = @basis.update{}
                  @basis
      ;; @basis.value = @dir.acquire{@basis.value}

   get{} =
      match @basis.value:
         Opaque? x -> x[contents]
         v -> v

   model{} =
      proxy{@basis.value, null, @patch}

   listen{model, patch} =
      @compute{patch}
      @commit{}


;;;;;;;;;;;;;;;;;;;
;; task-function ;;
;;;;;;;;;;;;;;;;;;;

task-function{render} =
   r = Reactor{render}
   method mkcache{args} =
      r.render{[@] or current-context{}.state, args}
   rval{*args} = mkcache{args}.value
   rval.orig = render
   rval.reactor = r
   rval.rend{*args} = mkcache{args}
   rval

opaque-task-function{render} =
   method render2{*args} =
      Opaque with render.apply{@, args}
   task-function{render2}


