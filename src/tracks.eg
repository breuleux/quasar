
require:
   .util ->
      ID

provide:
   Counter, Chirurgies, Tracks
   Patch


;;;;;;;;;;;;
;; Tracks ;;
;;;;;;;;;;;;


class Counter:
   constructor{} =
      @n-tracks = 0
   mark{} =
      @n-tracks += 1
   unmark{} =
      @n-tracks -= 1
   merge{other} =
      @n-tracks += other.n-tracks
   empty{} =
      @n-tracks <= 0
   not-empty{} =
      @n-tracks > 0
   size{} =
      1


class Chirurgies:
   constructor{} =
      @refs = new Map{}
   mark{{g, c}} =
      @refs.set{g, c}
   merge-one{counts, {g, c}} =
      if counts.n-tracks > 0:
         @refs.set{g, c}
   merge{other} =
      other.refs.entries{} each {g, c} -> @refs.set{g, c}
   unmerge-one{counts, {g, _}} =
      @refs.delete{g}
   unmerge{other} =
      other.refs.entries{} each {g, _} -> @refs.delete{g}
   unmerge-counts{other} =
      if other.n-tracks > 0:
         @refs = new Map{}
   empty{} =
      @refs.size === 0
   not-empty{} =
      @refs.size > 0
   size{} =
      @refs.size


class Tracks:

   constructor{@factory = Counter} =
      @data = {=}

   _data{} =
      object with items{@data} each {id, tr} ->
         {id} with
            object with items{tr} each {k, v} ->
               {k, v.not-empty{}}

   get{id} =
      id and [@data[id] or= {=}]
   get-for{obj} =
      if not obj: return null
      id = obj[ID]
      id and [@data[id] or= {=}]

   list{} =
      items{@data}
   empty{} =
      return keys{@data}.length === 0

   mark{obj, key} =
      tracks = @get-for{obj}
      if not tracks:
         return
      t = [tracks[key] or= @factory{}]
      t.mark{}

   unmark{obj, key} =
      tracks = @get-for{obj}
      if not tracks:
         return
      t = tracks[key]
      t.unmark{}
      if t.empty{}:
         delete tracks[key]

   merge{tracks, merger = .merge, arg = null} =
      items{tracks.data} each {id, v} ->
         v0 = [@data[id] or= {=}]
         items{v} each {key, obj} ->
            obj0 = [v0[key] or= @factory{}]
            obj0[merger]{obj, arg}
            if obj0.empty{}:
               delete v0[key]
         if keys{v0}.length === 0:
            delete @data[id]

   register{obj, key, fn} =
      val = fn{}
      if val !== true:
         throw E.cannot-assimilate-tracks{fn{}}
      @mark{obj, key}

   size{} =
      var sz = 0
      items{@data} each {id, ob} ->
         items{ob} each {k, v} ->
            sz += v.size{}
      sz



;;;;;;;;;;;
;; Patch ;;
;;;;;;;;;;;

class Patch:
   constructor{@data = {=}} =
      pass
   _data{} = @data
   get{id} =
      @data[id] or= {=}
   get-for{obj} =
      if not obj: return null
      id = obj[ID]
      id and [@data[id] or= {=}]
   register{obj, key, fn} =
      patch = @get-for{obj}
      if patch:
         patch[key] = fn{patch[key]}
   shift{new-data = {=}} =
      {expr-value, @data} = {@data, new-data}
   list{} =
      items{@data}
   empty{} =
      return keys{@data}.length === 0


